(in-package :coffee.umbrella.clarity.display)

(declaim (inline rendercode-thing))
(defun rendercode-thing (it)
  (declare (optimize (speed 3) (safety 1)))
  (etypecase it
    (single-float (ieee-floats:encode-float32 it))
    (fixnum       (ldb (byte 32 0) it))))

(defconstant instr.fillpoly 0)
(defconstant instr.cat-affine 1)
(defconstant instr.set-fill 2)
(defconstant instr.clear-screen 3)
(defconstant instr.fillqpoly 4)
(defconstant instr.ascii 5)
(defconstant instr.group-start 6)
(defconstant instr.group-end 7)
(defconstant instr.clipgroup-start 8)
(defconstant instr.translate 9)
(defconstant instr.rotate 10)
(defconstant instr.scale 11)
(defconstant instr.set-variable-width 12)
(defconstant instr.set-monospace 13)
(defconstant instr.set-font-size 14)
(defconstant instr.reserve-space 15)


(defrendercode start-group instr.group-start ())
(defrendercode start-clip-group instr.clipgroup-start ()
  (setf (rendercode-display-clip-next %display) t))
(defrendercode end-group instr.group-end ())
(defrendercode display-affine-concat instr.cat-affine (a)
  (& ((:v (a b c d e f) (affine:component-values a)))
     (<< a b c d e f)))
(defrendercode display-translate instr.translate (x y) (<< x y))
(defrendercode display-rotate instr.rotate (a) (<< a))
(defrendercode display-scale instr.scale (sx sy) (<< sx sy))
(defrendercode display-set-fill instr.set-fill (r g b a) (<< r g b a))
(defrendercode display-clear-screen instr.clear-screen ())
(defrendercode display-set-font-size instr.set-font-size (size) (<< size))
(defrendercode display-set-monospace instr.set-monospace ())
(defrendercode display-set-variable-width instr.set-variable-width ())
(defrendercode display-poly instr.fillpoly (&rest xs-and-ys) (<<<< xs-and-ys))
(defrendercode display-qpoly instr.fillqpoly (&rest xs-and-ys) (<<<< xs-and-ys))
(defrendercode display-reserve instr.reserve-space (&rest xs-and-ys) (<<<< xs-and-ys))
(defrendercode display-text instr.ascii (x y string)
  (locally (declare (type string string)
                    (type single-float x y)
                    (optimize (speed 3) (safety 0)))
    (let ((len (length string)))
      (<< x y len)
      (loop for i below len do
        (<<8 (char-code (char string i))))
      (loop repeat (- (* 4 (ceiling len 4)) len) do (<<8 0)))))


(define-compiler-macro display-poly (&whole form display &rest xs-and-ys)
  (if (symbolp display)
      (with-gensyms (out)
        `(if (typep ,display 'rendercode-display)
             (let ((,out (rendercode-display-codestream ,display)))
               (macrolet ((<< (x) (if (numberp x)
                                      `(write-byte ,(rendercode-thing x) ,',out)
                                      `(write-byte (rendercode-thing ,x) ,',out))))
                 (if (rendercode-display-clip-next ,display)
                     (setf (rendercode-display-clip-next ,display) nil)
                     (<< instr.fillpoly))
                 (<< ,(length xs-and-ys))
                 ,@(loop for num in xs-and-ys collect `(<< ,num))))
             (apply #'display-poly ,display (list ,@xs-and-ys))))
      form))

(define-compiler-macro display-qpoly (&whole form display &rest xs-and-ys)
  (if (symbolp display)
      (with-gensyms (out)
        `(if (typep ,display 'rendercode-display)
             (macrolet ((<< (x) (if (numberp x)
                                    `(write-byte ,(rendercode-thing x) ,',out)
                                    `(write-byte (rendercode-thing ,x) ,',out))))
               (let ((,out (rendercode-display-codestream ,display)))
                 (if (rendercode-display-clip-next ,display)
                     (setf (rendercode-display-clip-next ,display) nil)
                     (<< instr.fillqpoly))
                 (<< ,(length xs-and-ys))
                 ,@(loop for num in xs-and-ys collect `(<< ,num))))
             (apply #'display-qpoly ,display (list ,@xs-and-ys))))
      form))

(define-compiler-macro display-text (&whole form display x y string)
  (if (symbolp display)
      `(if (typep ,display 'rendercode-display)
           (macrolet ((<< (x)
                        (if (numberp x)
                            `(write-byte ,(rendercode-thing x) out)
                            `(write-byte (rendercode-thing ,x) out)))
                      (<<8 (x)
                        `(write-byte  ,x out8)))
             (let* ((out (rendercode-display-codestream ,display))
                    (out8 (slot-value out 'stream)))
               (<< instr.ascii)
               (let ((len (length ,string)))
                 (<< ,x) (<<  ,y) (<< len)
                 (loop for i below len do
                   (<<8 (char-code (char ,string i))))
                 (loop repeat (- (* 4 (ceiling len 4)) len) do (<<8 0)))))
           (apply #'display-text ,display ,x ,y (list ,string)))
      form))
